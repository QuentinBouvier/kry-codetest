plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '7.0.0'
  id "com.github.node-gradle.node" version "3.1.0"
}

ext {
  vertxVersion = '4.1.2'
  junitJupiterEngineVersion = '5.7.2'
  lombokVersion = '1.18.20'
  slf4jVersion = '1.7.31'
}

repositories {
  mavenLocal()
  maven {
    url 'https://repo.maven.apache.org/maven2'
    name 'Maven Central'
  }
}

group = 'se.kry'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = 11
mainClassName = 'io.vertx.core.Launcher'

def mainVerticleName = 'se.kry.codetest.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {

  implementation "io.vertx:vertx-core:$vertxVersion"
  implementation "io.vertx:vertx-core:$vertxVersion"
  implementation "io.vertx:vertx-web:$vertxVersion"
  implementation "io.vertx:vertx-web-client:$vertxVersion"
  implementation "io.vertx:vertx-config:$vertxVersion"
  implementation "io.vertx:vertx-jdbc-client:$vertxVersion"
  implementation "io.vertx:vertx-rx-java2:$vertxVersion"
  implementation 'org.apache.commons:commons-lang3:3.12.0'
  implementation "commons-validator:commons-validator:1.7"
  implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.36.0.1'
  implementation group: 'org.slf4j', name:'slf4j-simple', version: slf4jVersion
  implementation group: 'org.slf4j', name:'slf4j-api', version: slf4jVersion
  implementation 'joda-time:joda-time:2.10.10'

  compileOnly "org.projectlombok:lombok:$lombokVersion"
  annotationProcessor "org.projectlombok:lombok:$lombokVersion"
  testCompileOnly "org.projectlombok:lombok:$lombokVersion"
  testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

  testImplementation "io.vertx:vertx-junit5:$vertxVersion"
  testImplementation "io.vertx:vertx-junit5-rx-java2:$vertxVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junitJupiterEngineVersion"
  testCompileOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion"
}


shadowJar {
  archiveClassifier.set('fat')
  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events 'PASSED', 'FAILED', 'SKIPPED'
  }
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

node {
  version = "14.17.4"
  nodeModulesDir = file( "src/main/frontend")
  download = true
}

task buildFront(type: NpmTask) {
  dependsOn(nodeSetup, npmSetup, npmInstall)
  args = [ 'run', 'build' ]
}

task copyToWebRoot(type: Copy) {
  dependsOn(buildFront)
  from "src/main/frontend/dist"
  into "src/main/resources/webroot"
}

processResources {
  dependsOn(copyToWebRoot)
}

wrapper {
  gradleVersion = '7.1.1'
  distributionUrl = distributionUrl.replace("bin", "all")
}
